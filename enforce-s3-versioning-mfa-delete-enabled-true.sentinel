import "tfplan/v2" as tfplan

//find all aws_se_bucket that will be created
all_s3_buckets =  filter tfplan.resource_changes as _, rc {
		rc.type is "aws_s3_bucket" and
			rc.mode is "managed" and
    	rc.change.actions is ["create"]  
}

print(all_s3_buckets)


//make sure every bucket has a versioning block
no_default_versioning_block = rule {
	all all_s3_buckets as _, bucket {
		keys(bucket.change.after) contains "versioning"
	}
}

//make sure every bucket with versioning block has mfa_delete set as true
enforce_s3_versioning_mfa_delete = rule when no_default_versioning_block is true {
	all all_s3_buckets as _, bucket {
		all bucket.change.after.versioning as versioning {
			versioning.mfa_delete is true
		}
	}
}

main = rule {
	no_default_versioning_block and
	enforce_s3_versioning_mfa_delete
}